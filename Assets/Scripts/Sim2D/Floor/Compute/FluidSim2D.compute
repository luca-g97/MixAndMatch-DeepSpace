#pragma kernel ExternalForces
#pragma kernel UpdateSpatialHash
#pragma kernel Reorder
#pragma kernel ReorderCopyback
#pragma kernel CalculateDensities
#pragma kernel CalculatePressureForce
#pragma kernel CalculateViscosity
#pragma kernel UpdatePositions

#include "./FluidMaths2D.hlsl"
#include "./SpatialHash.hlsl"

#define FLT_MAX 3.402823466e+38
#define MAX_OBSTACLES 256

struct Obstacle
{
    float2 centre;
    int vertexStart;
    int vertexCount;
    float lineWidth;
    int obstacleType;
};

struct Current
{
    int vertexStart;
    int vertexCount;
    float maxVelocity;
    float width;
    float linearFactor;
};

StructuredBuffer<Obstacle> ObstaclesBuffer;
StructuredBuffer<float2> VerticesBuffer;
RWStructuredBuffer<int4> CollisionBuffer;
RWStructuredBuffer<int2> ParticleTypeBuffer;

int numObstacles;
float areaToColorAroundObstacles;
float coloredAreaAroundObstaclesDivider;
float minDistanceToRemoveParticles;
static const int NumThreads = 64;

StructuredBuffer<Current> CurrentsBuffer;
StructuredBuffer<float2> CurrentVerticesBuffer;
int numCurrents;

RWStructuredBuffer<float2> Positions;
RWStructuredBuffer<float2> PredictedPositions;
RWStructuredBuffer<float2> Velocities;
RWStructuredBuffer<float2> Densities;
StructuredBuffer<float> GravityScales;

RWStructuredBuffer<uint> SpatialKeys;
RWStructuredBuffer<uint> SpatialOffsets;
StructuredBuffer<uint> SortedIndices;

const uint numParticles;
const float gravity;
const float deltaTime;
const float collisionDamping;
const float smoothingRadius;

float waterTargetDensity;
float waterPressureMultiplier;
float waterNearPressureMultiplier;
float waterViscosityStrength;
float oilTargetDensity;
float oilPressureMultiplier;
float oilNearPressureMultiplier;
float oilViscosityStrength;

const float2 boundsSize;
const float yOffset;
const float2 interactionInputPoint;
const float interactionInputStrength;
const float interactionInputRadius;

const float mouseGravityStrength;
const float mouseGravityRadius;
const int invertMouseGravity;
const float2 mousePosition;
const int gKeyPressed;

float DensityKernel(float dst, float radius)
{
    return SpikyKernelPow2(dst, radius);
}
float NearDensityKernel(float dst, float radius)
{
    return SpikyKernelPow3(dst, radius);
}
float DensityDerivative(float dst, float radius)
{
    return DerivativeSpikyPow2(dst, radius);
}
float NearDensityDerivative(float dst, float radius)
{
    return DerivativeSpikyPow3(dst, radius);
}
float ViscosityKernel(float dst, float radius)
{
    return SmoothingKernelPoly6(dst, smoothingRadius);
}

float2 CalculateDensity(float2 pos)
{
    int2 originCell = GetCell2D(pos, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;
    float density = 0;
    float nearDensity = 0;

    for (int i = 0; i < 9; i++)
    {
        uint hash = HashCell2D(originCell + offsets2D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];

        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex++;

            uint neighbourKey = SpatialKeys[neighbourIndex];
            if (neighbourKey != key)
                break;

            float2 neighbourPos = PredictedPositions[neighbourIndex];
            float2 offsetToNeighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);

            if (sqrDstToNeighbour > sqrRadius)
                continue;

            float dst = sqrt(sqrDstToNeighbour);
            density += DensityKernel(dst, smoothingRadius);
            nearDensity += NearDensityKernel(dst, smoothingRadius);
        }
    }

    return float2(density, nearDensity);
}

float PressureFromDensity(float density, float targetDensity, float pressureMultiplier)
{
    return (density - targetDensity) * pressureMultiplier;
}
float NearPressureFromDensity(float nearDensity, float nearPressureMultiplier)
{
    return nearPressureMultiplier * nearDensity;
}

float2 ExternalForces(float2 pos, float2 velocity, float gravityScale)
{
    float2 gravityAccel = float2(0, gravity * gravityScale);

    if (interactionInputStrength != 0)
    {
        float2 inputPointOffset = interactionInputPoint - pos;
        float sqrDst = dot(inputPointOffset, inputPointOffset);
        if (sqrDst < interactionInputRadius * interactionInputRadius)
        {
            float dst = sqrt(sqrDst);
            float edgeT = (dst / interactionInputRadius);
            float centreT = 1 - edgeT;
            float2 dirToCentre = inputPointOffset / max(dst, 0.0001);

            float2 interactionForce = dirToCentre * interactionInputStrength * centreT;
            gravityAccel += interactionForce;
            gravityAccel -= velocity * centreT * 0.5;
        }
    }

    return gravityAccel;
}

// Optimized HandleCollisions - Focused on reducing expensive operations (sqrt, normalization)
// and minimizing work inside the inner vertex loop after performance degradation was observed.
void HandleCollisions(uint particleIndex)
{
    // Initialize Process Flag for this particle for this collision step IF it's not already marked for removal globally.
    // This ensures that if a particle wasn't marked in a previous step, it starts fresh for this obstacle interaction.
    // However, if already marked by a previous obstacle in *this same HandleCollisions pass*, we don't want to reset.
    // The CPU will handle resetting flags globally after processing removals.
    // So, we only write to ParticleProcessFlags[particleIndex] upon detection.

    float2 pos = Positions[particleIndex];
    float2 vel = Velocities[particleIndex];
    int particleType = ParticleTypeBuffer[particleIndex][0];
    int particleProcessFlag = ParticleTypeBuffer[particleIndex][1];

    // If particle is already marked as non-participating (e.g. type -1), skip complex collision.
    // This check is vital if ParticleTypeBuffer can be set to -1 mid-step by this function.
    if (particleType < 0)
    {
        // Still perform boundary collisions for "dead" particles if desired, or just return.
        // For now, let's assume "dead" particles don't interact further.
        // However, boundary checks might still be useful to prevent them flying off to infinity
        // before CPU collects them. For simplicity in this change, we'll let them be.
        // The main boundary collision happens *after* this loop for all particles.
        // Re-applying boundary for already "removed" particles:
        const float2 halfSizeMinimal = boundsSize * 0.5;
        const float boundaryEpsilonMinimal = 0.001f;
        if (abs(pos.x) >= halfSizeMinimal.x)
        {
            pos.x = (halfSizeMinimal.x - boundaryEpsilonMinimal) * sign(pos.x);
            vel.x *= -collisionDamping;
        }
        if (abs(pos.y) >= halfSizeMinimal.y)
        {
            pos.y = (halfSizeMinimal.y - boundaryEpsilonMinimal) * sign(pos.y);
            vel.y *= -collisionDamping;
        }
        Positions[particleIndex] = pos; // Write back potentially adjusted position/velocity
        Velocities[particleIndex] = vel;
        return;
    }


    // --- Pre-calculations (Avoid redundant calculations later) ---
    const float2 halfSize = boundsSize * 0.5;
    const float boundaryEpsilon = 0.001f; // Small offset for boundary push-off

    float areaToColorAroundObstaclesSq = areaToColorAroundObstacles * areaToColorAroundObstacles;
    float secondaryColorDist = areaToColorAroundObstacles + coloredAreaAroundObstaclesDivider;
    float secondaryColorDistSq = secondaryColorDist * secondaryColorDist;

    int collisionIndices[4] = { -1, -1, -1, -1 };
    int consideredObstacles = 0;

    // --- 1. Boundary Collisions ---
    if (abs(pos.x) >= halfSize.x)
    {
        pos.x = (halfSize.x - boundaryEpsilon) * sign(pos.x);
        vel.x *= -collisionDamping;
    }
    if (abs(pos.y) >= halfSize.y)
    {
        pos.y = (halfSize.y - boundaryEpsilon) * sign(pos.y);
        vel.y *= -collisionDamping;
    }

    // --- 2. Obstacle Collisions ---
    [loop]
    for (int obstacleIdx = 0; obstacleIdx < numObstacles && obstacleIdx < MAX_OBSTACLES; obstacleIdx++)
    {
        Obstacle obstacle = ObstaclesBuffer[obstacleIdx];
        int vertexStart = obstacle.vertexStart;
        int vertexCount = obstacle.vertexCount;

        if (vertexCount < 2)
            continue;

        float lineWidth = obstacle.lineWidth;
        float particleRadius = 0.1f; // TODO: Consider making this a uniform
        float collisionPadding = particleRadius - (lineWidth * 0.5f); //(lineWidth * 0.5f);
        float collisionPaddingSq = collisionPadding * collisionPadding;

        int intersections = 0;
        float minDistSq = FLT_MAX;
        float2 normalAtClosestPoint = float2(0, 0);

        [loop]
        for (int j = 0; j < vertexCount; j++)
        {
            int nextJ = uint(j + 1) % uint(vertexCount);
            float2 a = VerticesBuffer[vertexStart + j];
            float2 b = VerticesBuffer[vertexStart + nextJ];
            float2 edge = b - a;
            float2 vecToA = pos - a;

            float edgeLengthSq = dot(edge, edge);
            float2 pointOnEdge;
            if (edgeLengthSq > 1e-6f)
            {
                float t = clamp(dot(vecToA, edge) / edgeLengthSq, 0.0, 1.0);
                pointOnEdge = a + t * edge;
            }
            else
            {
                pointOnEdge = a;
            }

            float2 offsetToEdgePoint = pos - pointOnEdge;
            float distSq = dot(offsetToEdgePoint, offsetToEdgePoint);

            if (distSq < minDistSq)
            {
                minDistSq = distSq;
                if (distSq > 1e-6f)
                {
                    normalAtClosestPoint = normalize(offsetToEdgePoint);
                }
                else
                {
                    if (edgeLengthSq > 1e-6f)
                    {
                        float2 edgeDir = normalize(edge);
                        normalAtClosestPoint = normalize(float2(-edgeDir.y, edgeDir.x));
                    }
                    else
                    {
                        normalAtClosestPoint = float2(0, 1);
                    }
                }
            }

            if (vertexCount >= 3 && ((a.y <= pos.y && pos.y < b.y) || (b.y <= pos.y && pos.y < a.y)))
            {
                if (abs(b.y - a.y) > 1e-6f)
                {
                    float intersectX = a.x + (b.x - a.x) * (pos.y - a.y) / (b.y - a.y);
                    if (pos.x < intersectX)
                    {
                        intersections++;
                    }
                }
            }
        } // --- End Inner Vertex Loop ---

        bool isInside = (vertexCount >= 3) && ((uint(intersections) % 2) != 0);
        bool collisionDetected = isInside || (minDistSq < collisionPaddingSq);

        // --- Particle Removal and Coloring Logic ---
        // Check for removal condition FIRST. If removed, it might not need coloring.
        // Ensure particleType is still valid (>=0) before processing for removal by this obstacle.
        // ParticleProcessFlags[particleIndex] == 0 ensures we only mark it once per frame by the first Ventil/Player it hits.
        if (particleProcessFlag == -1 && (minDistSq <= minDistanceToRemoveParticles) && particleType > 0 && (obstacle.obstacleType == 0 || obstacle.obstacleType == 2))
        {
            if (obstacle.obstacleType == 0) // Player
            {
                ParticleTypeBuffer[particleIndex][1] = obstacleIdx; // Mark for removal by Player
            }
            else if(obstacle.obstacleType == 2 && isInside) //Only remove on collision
            {
                ParticleTypeBuffer[particleIndex][1] = -2; // Mark for removal by Ventil
            }
        }


        // Coloring Logic (only if not marked for removal by this obstacle pass or is not a Player obstacle)
        // Check particleType again because it might have been set to -1 by the removal logic above.
        // And ensure ParticleProcessFlags is still 0, meaning it wasn't just marked for removal by *this* obstacle.
        // (If it was marked by a *previous* obstacle in this loop, particleType would already be -1, failing the ParticleTypeBuffer[particleIndex] >=0 check for coloring)
        if (ParticleTypeBuffer[particleIndex][1] != -2 && particleType > 0 && obstacle.obstacleType == 0) // Only color if particle is active and obstacle is a player
        {
            if (consideredObstacles < 4)
            {
                if (minDistSq < areaToColorAroundObstaclesSq)
                {
                    collisionIndices[consideredObstacles] = obstacleIdx;
                    consideredObstacles++;
                }
                else if (minDistSq < secondaryColorDistSq)
                {
                    collisionIndices[consideredObstacles] = -2; // Special marker for secondary color
                    consideredObstacles++;
                }
            }
        }


        // --- Collision Response (only for physical obstacles: type 0 Player, type 1 Static Obstacle) ---
        // And only if particle is still considered active (ParticleTypeBuffer[particleIndex] >= 0)
        if (particleType >= 0 && collisionDetected && (obstacle.obstacleType == 0 || obstacle.obstacleType == 1))
        {
            float actualDistance = sqrt(minDistSq);
            float penetrationDepth;
            if (isInside)
            {
                penetrationDepth = actualDistance + collisionPadding;
            }
            else
            {
                penetrationDepth = collisionPadding - actualDistance;
            }
            penetrationDepth += 0.001f;

            pos += normalAtClosestPoint * penetrationDepth;

            float normalSpeed = dot(vel, normalAtClosestPoint);
            if (normalSpeed < 0)
            {
                float2 tangentVel = vel - normalAtClosestPoint * normalSpeed;
                float2 normalVel = -normalAtClosestPoint * normalSpeed * collisionDamping;
                float friction = 0.1f;
                vel = tangentVel * (1.0f - friction) + normalVel;
            }
        } // --- End Collision Response ---
    } // --- End Outer Obstacle Loop ---
    
    CollisionBuffer[particleIndex] = int4(collisionIndices[0], collisionIndices[1], collisionIndices[2], collisionIndices[3]);
    Positions[particleIndex] = pos;
    Velocities[particleIndex] = vel;
}

[numthreads(NumThreads, 1, 1)]
void ExternalForces(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    float gravityScale = GravityScales[id.x];
    float2 pos = Positions[id.x];
    float2 toMouse = mousePosition - pos;
    float mouseDist = length(toMouse);

    if (mouseDist < mouseGravityRadius && mouseDist > 0 && gKeyPressed)
    {
        float2 mouseDir = toMouse / mouseDist;
        float falloff = 1 - (mouseDist / mouseGravityRadius);
        float mouseInfluence = mouseGravityStrength * falloff;
        if (invertMouseGravity)
            mouseInfluence *= -1;
        gravityScale += mouseInfluence;
    }

    Velocities[id.x] += ExternalForces(pos, Velocities[id.x], gravityScale) * deltaTime;
    PredictedPositions[id.x] = pos + Velocities[id.x] * (1 / 120.0);
}

[numthreads(NumThreads, 1, 1)]
void UpdateSpatialHash(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;
    uint index = id.x;
    int2 cell = GetCell2D(PredictedPositions[index], smoothingRadius);
    uint hash = HashCell2D(cell);
    uint key = KeyFromHash(hash, numParticles);
    SpatialKeys[id.x] = key;
}

void ApplyCurrentForces(uint particleIndex, float2 pos)
{    
    for (int currentIdx = 0; currentIdx < numCurrents; currentIdx++)
    {        
        Current current = CurrentsBuffer[currentIdx];
        int numSegments = current.vertexCount - 1;
        
        if (numSegments < 1)
            continue;
        
        float closestDistSq = FLT_MAX;
        float2 closestDir = float2(0, 0);
        float maxStrength = 0;
        
        for (int segIdx = 0; segIdx < numSegments; segIdx++)
        {
            int pointIdx = current.vertexStart + segIdx;
            float2 a = CurrentVerticesBuffer[pointIdx];
            float2 b = CurrentVerticesBuffer[pointIdx + 1];
            
            float2 ab = b - a;
            float2 ap = pos - a;
            float lin_factor = clamp(dot(ap, ab) / dot(ab, ab), 0.0, 1.0);
            float2 closestPoint = a + lin_factor * ab;
            float2 toClosest = pos - closestPoint;
            float distSq = dot(toClosest, toClosest);
            
            if (distSq < current.width * current.width)
            {
                float dist = sqrt(distSq);
                float normalizedDist = dist / current.width;
                float strength = pow(1.0 - normalizedDist, 1.0 + current.linearFactor);
                
                if (strength > maxStrength)
                {
                    maxStrength = strength;
                    closestDir = normalize(ab);
                    closestDistSq = distSq;
                }
            }
        }
        
        if (maxStrength > 0.001)
        {
            Velocities[particleIndex] += closestDir * current.maxVelocity * maxStrength;
        }
    } 
}

RWStructuredBuffer<float2> SortTarget_Positions;
RWStructuredBuffer<float2> SortTarget_PredictedPositions;
RWStructuredBuffer<float2> SortTarget_Velocities;
RWStructuredBuffer<int2> SortTarget_ParticleType;

[numthreads(NumThreads, 1, 1)]
void Reorder(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;
    uint sortedIndex = SortedIndices[id.x];
    SortTarget_Positions[id.x] = Positions[sortedIndex];
    SortTarget_PredictedPositions[id.x] = PredictedPositions[sortedIndex];
    SortTarget_Velocities[id.x] = Velocities[sortedIndex];
    SortTarget_ParticleType[id.x] = ParticleTypeBuffer[sortedIndex];
}

[numthreads(NumThreads, 1, 1)]
void ReorderCopyback(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles || ParticleTypeBuffer[id.x][0] == -1)
        return;
    Positions[id.x] = SortTarget_Positions[id.x];
    PredictedPositions[id.x] = SortTarget_PredictedPositions[id.x];
    Velocities[id.x] = SortTarget_Velocities[id.x];
    ParticleTypeBuffer[id.x] = SortTarget_ParticleType[id.x];
}

[numthreads(NumThreads, 1, 1)]
void CalculateDensities(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles || ParticleTypeBuffer[id.x][0] == -1)
        return;
    float2 pos = PredictedPositions[id.x];
    Densities[id.x] = CalculateDensity(pos);
}

[numthreads(NumThreads, 1, 1)]
void CalculatePressureForce(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles || ParticleTypeBuffer[id.x][0] == -1)
        return;
    
    float targetDensity = ParticleTypeBuffer[id.x][0] == 0 ? waterTargetDensity : oilTargetDensity;
    float pressureMultiplier = ParticleTypeBuffer[id.x][0] == 0 ? waterPressureMultiplier : oilPressureMultiplier;
    float nearPressureMultiplier = ParticleTypeBuffer[id.x][0] == 0 ? waterNearPressureMultiplier : oilNearPressureMultiplier;
    float density = Densities[id.x][0];
    float densityNear = Densities[id.x][1];
    float pressure = PressureFromDensity(density, targetDensity, pressureMultiplier);
    float nearPressure = NearPressureFromDensity(densityNear, nearPressureMultiplier);
    float2 pressureForce = 0;

    float2 pos = PredictedPositions[id.x];
    int2 originCell = GetCell2D(pos, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;

    for (int i = 0; i < 9; i++)
    {        
        uint hash = HashCell2D(originCell + offsets2D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];

        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex++;

            if (neighbourIndex == id.x)
                continue;
            uint neighbourKey = SpatialKeys[neighbourIndex];
            if (neighbourKey != key)
                break;

            float2 neighbourPos = PredictedPositions[neighbourIndex];
            float2 offsetToNeighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);

            if (sqrDstToNeighbour > sqrRadius)
                continue;

            float dst = sqrt(sqrDstToNeighbour);
            float2 dirToNeighbour = dst > 0 ? offsetToNeighbour / dst : float2(0, 1);
            
            float neighbourTargetDensity = ParticleTypeBuffer[neighbourIndex][0] == 0 ? waterTargetDensity : oilTargetDensity;
            float neighbourPressureMultiplier = ParticleTypeBuffer[neighbourIndex][0] == 0 ? waterPressureMultiplier : oilPressureMultiplier;
            float neighbourNearPressureMultiplier = ParticleTypeBuffer[neighbourIndex][0] == 0 ? waterNearPressureMultiplier : oilNearPressureMultiplier;

            float neighbourDensity = Densities[neighbourIndex][0];
            float neighbourNearDensity = Densities[neighbourIndex][1];
            float neighbourPressure = PressureFromDensity(neighbourDensity, neighbourTargetDensity-density, neighbourPressureMultiplier);
            float neighbourNearPressure = NearPressureFromDensity(neighbourNearDensity, neighbourNearPressureMultiplier);

            float sharedPressure = (pressure + neighbourPressure) * 0.5;
            float sharedNearPressure = (nearPressure + neighbourNearPressure) * 0.5;

            pressureForce += dirToNeighbour * DensityDerivative(dst, smoothingRadius) * sharedPressure / neighbourDensity;
            pressureForce += dirToNeighbour * NearDensityDerivative(dst, smoothingRadius) * sharedNearPressure / neighbourNearDensity;
        }
    }

    float2 acceleration = pressureForce / density;
    Velocities[id.x] += acceleration * deltaTime;
}

[numthreads(NumThreads, 1, 1)]
void CalculateViscosity(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles || ParticleTypeBuffer[id.x][0] == -1)
        return;

    float2 pos = PredictedPositions[id.x];
    int2 originCell = GetCell2D(pos, smoothingRadius);
    float sqrRadius = smoothingRadius * smoothingRadius;
    float2 viscosityForce = 0;
    float2 velocity = Velocities[id.x];

    for (int i = 0; i < 9; i++)
    {
        uint hash = HashCell2D(originCell + offsets2D[i]);
        uint key = KeyFromHash(hash, numParticles);
        uint currIndex = SpatialOffsets[key];

        while (currIndex < numParticles)
        {
            uint neighbourIndex = currIndex;
            currIndex++;

            if (neighbourIndex == id.x)
                continue;
            uint neighbourKey = SpatialKeys[neighbourIndex];
            if (neighbourKey != key)
                break;

            float2 neighbourPos = PredictedPositions[neighbourIndex];
            float2 offsetToNeighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);

            if (sqrDstToNeighbour > sqrRadius)
                continue;

            float dst = sqrt(sqrDstToNeighbour);
            float2 neighbourVelocity = Velocities[neighbourIndex];
            viscosityForce += (neighbourVelocity - velocity) * ViscosityKernel(dst, smoothingRadius);
        }
    }
    
    float viscosityStrength = ParticleTypeBuffer[id.x][0] == 0 ? waterViscosityStrength : oilViscosityStrength;
    Velocities[id.x] += viscosityForce * viscosityStrength * deltaTime;
}

[numthreads(NumThreads, 1, 1)]
void UpdatePositions(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    ApplyCurrentForces(id.x, Positions[id.x]);
    HandleCollisions(id.x);
    
    Positions[id.x] += Velocities[id.x] * deltaTime;
}