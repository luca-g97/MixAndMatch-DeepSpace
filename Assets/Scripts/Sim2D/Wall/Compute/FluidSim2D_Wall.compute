#pragma kernel ExternalForces_Wall
#pragma kernel UpdateSpatialHash_Wall
#pragma kernel Reorder_Wall
#pragma kernel ReorderCopyback_Wall
#pragma kernel CalculateDensities_Wall
#pragma kernel CalculatePressureForce_Wall
#pragma kernel CalculateViscosity_Wall
#pragma kernel UpdatePositions_Wall
#pragma kernel ResetCompactionCounter_Wall
#pragma kernel CompactAndMoveParticles_Wall
#pragma kernel CopyCollisionBuffer_Wall

#include "./FluidMaths2D_Wall.hlsl"
#include "./SpatialHash_Wall.hlsl"

#define FLT_MAX 3.402823466e+38
#define MAX_OBSTACLES 256

// --- Structs ---
struct VentilatedParticleData
{
    float2 pos;
    float2 vel;
    int2 type;
};

struct Obstacle
{
    float2 centre;
    int vertexStart;
    int vertexCount;
    float lineWidth;
    int obstacleType;
};

struct Current
{
    int vertexStart;
    int vertexCount;
    float maxVelocity;
    float width;
    float linearFactor;
};

// --- Buffers ---
RWStructuredBuffer<float2> Positions_Wall;
RWStructuredBuffer<float2> PredictedPositions_Wall;
RWStructuredBuffer<float2> Velocities_Wall;
RWStructuredBuffer<float2> Densities_Wall;
RWStructuredBuffer<int4> CollisionBuffer_Wall;
RWStructuredBuffer<int2> ParticleTypeBuffer_Wall;
RWStructuredBuffer<uint> SpatialKeys_Wall;
RWStructuredBuffer<uint> SpatialOffsets_Wall;
RWStructuredBuffer<int4> CopiedCollisionBuffer_Destination_Wall;

StructuredBuffer<float> GravityScales_Wall;
StructuredBuffer<uint> SortedIndices_Wall;
StructuredBuffer<Obstacle> ObstaclesBuffer_Wall;
StructuredBuffer<float2> VerticesBuffer_Wall;
StructuredBuffer<Current> CurrentsBuffer_Wall;
StructuredBuffer<float2> CurrentVerticesBuffer_Wall;
StructuredBuffer<int4> OriginalCollisionBuffer_Source_Wall;

RWStructuredBuffer<float2> SortTarget_Positions_Wall;
RWStructuredBuffer<float2> SortTarget_PredictedPositions_Wall;
RWStructuredBuffer<float2> SortTarget_Velocities_Wall;
RWStructuredBuffer<int2> SortTarget_ParticleType_Wall;
RWStructuredBuffer<int4> SortTarget_Collision_Wall;
RWStructuredBuffer<uint> CompactionInfoBuffer_Wall;
StructuredBuffer<float4> ObstacleColorsBuffer_Wall;

StructuredBuffer<float2> Source_Positions_Wall;
StructuredBuffer<float2> Source_PredictedPositions_Wall;
StructuredBuffer<float2> Source_Velocities_Wall;
StructuredBuffer<int2> Source_ParticleType_Wall;
StructuredBuffer<int4> Source_Collision_Wall;

StructuredBuffer<float2> CopySource_Positions_Wall;
StructuredBuffer<float2> CopySource_PredictedPositions_Wall;
StructuredBuffer<float2> CopySource_Velocities_Wall;
StructuredBuffer<int2> CopySource_ParticleType_Wall;
StructuredBuffer<int4> CopySource_Collision_Wall;

RWStructuredBuffer<VentilatedParticleData> VentilatedParticlesBuffer_Wall;
RWStructuredBuffer<uint> VentilatedParticleCount_Wall;

// --- Global Variables ---
int numObstacles_Wall;
float areaToColorAroundObstacles_Wall;
float coloredAreaAroundObstaclesDivider_Wall;
float minDistanceToRemoveParticles_Wall;
static const int NumThreads_Wall = 64;
int numCurrents_Wall;

float4 mixableColors_Wall[12];
int mixableColorsSize_Wall;
float4 colorPalette_Wall[12];
int colorPaletteSize_Wall;

const uint numParticles_Wall;
const float gravity_Wall;
const float deltaTime_Wall;
const float collisionDamping_Wall;
const float smoothingRadius_Wall;

float waterTargetDensity_Wall;
float waterPressureMultiplier_Wall;
float waterNearPressureMultiplier_Wall;
float waterViscosityStrength_Wall;
float oilTargetDensity_Wall;
float oilPressureMultiplier_Wall;
float oilNearPressureMultiplier_Wall;
float oilViscosityStrength_Wall;

const float2 boundsSize_Wall;
const float yOffset_Wall;
const float2 interactionInputPoint_Wall;
const float interactionInputStrength_Wall;
const float interactionInputRadius_Wall;

const float mouseGravityStrength_Wall;
const float mouseGravityRadius_Wall;
const int invertMouseGravity_Wall;
const float2 mousePosition_Wall;
const int gKeyPressed_Wall;

// --- Helper Functions ---
float DensityKernel_Wall(float dst, float radius)
{
    return SpikyKernelPow2_Wall(dst, radius);
}
float NearDensityKernel_Wall(float dst, float radius)
{
    return SpikyKernelPow3_Wall(dst, radius);
}
float DensityDerivative_Wall(float dst, float radius)
{
    return DerivativeSpikyPow2_Wall(dst, radius);
}
float NearDensityDerivative_Wall(float dst, float radius)
{
    return DerivativeSpikyPow3_Wall(dst, radius);
}
float ViscosityKernel_Wall(float dst, float radius)
{
    return SmoothingKernelPoly6_Wall(dst, smoothingRadius_Wall);
}

float2 CalculateDensity_Wall(float2 pos)
{
    int2 originCell = GetCell2D_Wall(pos, smoothingRadius_Wall);
    float sqrRadius = smoothingRadius_Wall * smoothingRadius_Wall;
    float density = 0;
    float nearDensity = 0;
    for (int i = 0; i < 9; i++)
    {
        uint hash = HashCell2D_Wall(originCell + offsets2D_Wall[i]);
        uint key = KeyFromHash_Wall(hash, numParticles_Wall);
        uint currIndex = SpatialOffsets_Wall[key];
        while (currIndex < numParticles_Wall)
        {
            uint neighbourIndex = currIndex;
            currIndex++;
            uint neighbourKey = SpatialKeys_Wall[neighbourIndex];
            if (neighbourKey != key)
                break;
            float2 neighbourPos = PredictedPositions_Wall[neighbourIndex];
            float2 offsetToNeighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);
            if (sqrDstToNeighbour > sqrRadius)
                continue;
            float dst = sqrt(sqrDstToNeighbour);
            density += DensityKernel_Wall(dst, smoothingRadius_Wall);
            nearDensity += NearDensityKernel_Wall(dst, smoothingRadius_Wall);
        }
    }
    return float2(density, nearDensity);
}

float PressureFromDensity_Wall(float density, float targetDensity, float pressureMultiplier)
{
    return (density - targetDensity) * pressureMultiplier;
}
float NearPressureFromDensity_Wall(float nearDensity, float nearPressureMultiplier)
{
    return nearPressureMultiplier * nearDensity;
}

float2 ExternalForces_Func_Wall(float2 pos, float2 velocity, float gravityScale)
{
    float2 gravityAccel = float2(0, gravity_Wall * gravityScale);
    if (interactionInputStrength_Wall != 0)
    {
        float2 inputPointOffset = interactionInputPoint_Wall - pos;
        float sqrDst = dot(inputPointOffset, inputPointOffset);
        if (sqrDst < interactionInputRadius_Wall * interactionInputRadius_Wall)
        {
            float dst = sqrt(sqrDst);
            float edgeT = (dst / interactionInputRadius_Wall);
            float centreT = 1 - edgeT;
            float2 dirToCentre = inputPointOffset / max(dst, 0.0001);
            float2 interactionForce = dirToCentre * interactionInputStrength_Wall * centreT;
            gravityAccel += interactionForce;
            gravityAccel -= velocity * centreT * 0.5;
        }
    }
    return gravityAccel;
}

void HandleCollisions_Wall(uint particleIndex)
{
    float2 pos = Positions_Wall[particleIndex];
    float2 vel = Velocities_Wall[particleIndex];
    int particleType = ParticleTypeBuffer_Wall[particleIndex][0];
    int particleProcessFlag = ParticleTypeBuffer_Wall[particleIndex][1];

    if (particleType < 0)
    {
        const float2 halfSizeMinimal = boundsSize_Wall * 0.5;
        const float boundaryEpsilonMinimal = 0.001f;
        if (abs(pos.x) >= halfSizeMinimal.x)
        {
            pos.x = (halfSizeMinimal.x - boundaryEpsilonMinimal) * sign(pos.x);
            vel.x *= -collisionDamping_Wall;
        }
        if (pos.y >= halfSizeMinimal.y + yOffset_Wall)
        {
            pos.y = (halfSizeMinimal.y - boundaryEpsilonMinimal) * sign(pos.y) + yOffset_Wall;
            vel.y *= -collisionDamping_Wall;
        }
        if (pos.y <= -halfSizeMinimal.y + yOffset_Wall)
        {
            pos.y = (-halfSizeMinimal.y - boundaryEpsilonMinimal) * sign(pos.y) + yOffset_Wall;
            vel.y *= -collisionDamping_Wall;
        }
        Positions_Wall[particleIndex] = pos;
        Velocities_Wall[particleIndex] = vel;
        return;
    }

    const float2 halfSize = boundsSize_Wall * 0.5;
    const float boundaryEpsilon = 0.001f;
    float areaToColorAroundObstaclesSq = areaToColorAroundObstacles_Wall * areaToColorAroundObstacles_Wall;
    float minDistanceToRemoveParticlesSq = minDistanceToRemoveParticles_Wall * minDistanceToRemoveParticles_Wall;
    float secondaryColorDist = areaToColorAroundObstacles_Wall + coloredAreaAroundObstaclesDivider_Wall;
    float secondaryColorDistSq = secondaryColorDist * secondaryColorDist;

    int collisionIndices[4] = { -1, -1, -1, -1 };
    int consideredObstacles = 0;

    if (abs(pos.x) >= halfSize.x)
    {
        pos.x = (halfSize.x - boundaryEpsilon) * sign(pos.x);
        vel.x *= -collisionDamping_Wall;
    }
    if (pos.y >= halfSize.y + yOffset_Wall)
    {
        pos.y = (halfSize.y - boundaryEpsilon) * sign(pos.y) + yOffset_Wall;
        vel.y *= -collisionDamping_Wall;
    }
    if (pos.y <= -halfSize.y + yOffset_Wall)
    {
        pos.y = (-halfSize.y - boundaryEpsilon) * sign(pos.y) + yOffset_Wall;
        vel.y *= -collisionDamping_Wall;
    }

    [loop]
    for (int obstacleIdx = 0; obstacleIdx < numObstacles_Wall && obstacleIdx < MAX_OBSTACLES; obstacleIdx++)
    {
        Obstacle obstacle = ObstaclesBuffer_Wall[obstacleIdx];
        if (obstacle.vertexCount < 2)
            continue;
        int intersections = 0;
        float minDistSq = FLT_MAX;
        float2 normalAtClosestPoint = float2(0, 0);
        [loop]
        for (int j = 0; j < obstacle.vertexCount; j++)
        {
            int nextJ = (uint) (j + 1) % (uint) obstacle.vertexCount;
            float2 a = VerticesBuffer_Wall[obstacle.vertexStart + j];
            float2 b = VerticesBuffer_Wall[obstacle.vertexStart + nextJ];
            float2 edge = b - a;
            float2 vecToA = pos - a;
            float edgeLengthSq = dot(edge, edge);
            float2 pointOnEdge = a + clamp(dot(vecToA, edge) / max(edgeLengthSq, 1e-6f), 0.0, 1.0) * edge;
            float2 offsetToPoint = pos - pointOnEdge;
            float distSq = dot(offsetToPoint, offsetToPoint);
            if (distSq < minDistSq)
            {
                minDistSq = distSq;
                if (distSq > 1e-6f)
                {
                    normalAtClosestPoint = normalize(pos - pointOnEdge);
                }
                else
                {
                    float2 edgeDir = normalize(edge);
                    normalAtClosestPoint = normalize(float2(-edgeDir.y, edgeDir.x));
                }
            }
            if (obstacle.vertexCount >= 3 && ((a.y <= pos.y && pos.y < b.y) || (b.y <= pos.y && pos.y < a.y)))
            {
                if (abs(b.y - a.y) > 1e-6f)
                {
                    if (pos.x < a.x + (b.x - a.x) * (pos.y - a.y) / (b.y - a.y))
                    {
                        intersections++;
                    }
                }
            }
        }
        bool isInside = (obstacle.vertexCount >= 3) && ((uint(intersections) % 2) != 0);
        if (particleType > 0)
        {
            if (obstacle.obstacleType == 0)
            {
                if (consideredObstacles < 4)
                {
                    if (minDistSq < areaToColorAroundObstaclesSq)
                    {
                        collisionIndices[consideredObstacles] = obstacleIdx;
                        consideredObstacles++;
                    }
                    else if (minDistSq < secondaryColorDistSq)
                    {
                        collisionIndices[consideredObstacles] = -2;
                        consideredObstacles++;
                    }
                }
                if (particleProcessFlag == -1 && minDistSq <= minDistanceToRemoveParticlesSq)
                {
                    ParticleTypeBuffer_Wall[particleIndex][1] = obstacleIdx;
                    break;
                }
            }
            else if (obstacle.obstacleType == 2 && particleProcessFlag == -1 && isInside)
            {
                ParticleTypeBuffer_Wall[particleIndex][1] = -obstacleIdx - 2;
            }
        }
        float particleRadius = 0.1f;
        float collisionPadding = particleRadius - (obstacle.lineWidth * 0.5f);
        bool collisionDetected = isInside || (minDistSq < collisionPadding * collisionPadding);
        if (particleType >= 0 && collisionDetected && (obstacle.obstacleType == 0 || obstacle.obstacleType == 1))
        {
            float actualDistance = sqrt(minDistSq);
            float penetrationDepth = isInside ? (actualDistance + collisionPadding) : (collisionPadding - actualDistance);
            pos += normalAtClosestPoint * (penetrationDepth + 0.001f);
            float normalSpeed = dot(vel, normalAtClosestPoint);
            if (normalSpeed < 0)
            {
                float2 tangentVel = vel - normalAtClosestPoint * normalSpeed;
                float2 normalVel = -normalAtClosestPoint * normalSpeed * collisionDamping_Wall;
                float friction = 0.1f;
                vel = tangentVel * (1.0f - friction) + normalVel;
            }
        }
    }
    CollisionBuffer_Wall[particleIndex] = int4(collisionIndices[0], collisionIndices[1], collisionIndices[2], collisionIndices[3]);
    Positions_Wall[particleIndex] = pos;
    Velocities_Wall[particleIndex] = vel;
}

void ApplyCurrentForces_Wall(uint particleIndex, float2 pos)
{
    for (int currentIdx = 0; currentIdx < numCurrents_Wall; currentIdx++)
    {
        Current current = CurrentsBuffer_Wall[currentIdx];
        int numSegments = current.vertexCount - 1;
        if (numSegments < 1)
            continue;
        float maxStrength = 0;
        float2 closestDir = float2(0, 0);
        for (int segIdx = 0; segIdx < numSegments; segIdx++)
        {
            int pointIdx = current.vertexStart + segIdx;
            float2 a = CurrentVerticesBuffer_Wall[pointIdx];
            float2 b = CurrentVerticesBuffer_Wall[pointIdx + 1];
            float2 ab = b - a;
            float2 ap = pos - a;
            float lin_factor = clamp(dot(ap, ab) / dot(ab, ab), 0.0, 1.0);
            float2 closestPoint = a + lin_factor * ab;
            float2 toClosest = pos - closestPoint;
            float distSq = dot(toClosest, toClosest);
            if (distSq < current.width * current.width)
            {
                float dist = sqrt(distSq);
                float normalizedDist = dist / current.width;
                float strength = pow(abs(1.0 - normalizedDist), abs(1.0 + current.linearFactor));
                if (strength > maxStrength)
                {
                    maxStrength = strength;
                    closestDir = normalize(ab);
                }
            }
        }
        if (maxStrength > 0.001)
        {
            Velocities_Wall[particleIndex] += closestDir * current.maxVelocity * maxStrength;
        }
    }
}


// --- Kernel Entry Points ---
[numthreads(NumThreads_Wall, 1, 1)]
void ExternalForces_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    float gravityScale = GravityScales_Wall[id.x];
    float2 pos = Positions_Wall[id.x];
    float2 toMouse = mousePosition_Wall - pos;
    float mouseDist = length(toMouse);
    if (mouseDist < mouseGravityRadius_Wall && mouseDist > 0 && gKeyPressed_Wall)
    {
        float2 mouseDir = toMouse / mouseDist;
        float falloff = 1 - (mouseDist / mouseGravityRadius_Wall);
        float mouseInfluence = mouseGravityStrength_Wall * falloff;
        if (invertMouseGravity_Wall)
            mouseInfluence *= -1;
        gravityScale += mouseInfluence;
    }
    Velocities_Wall[id.x] += ExternalForces_Func_Wall(pos, Velocities_Wall[id.x], gravityScale) * deltaTime_Wall;
    PredictedPositions_Wall[id.x] = pos + Velocities_Wall[id.x] * (1 / 120.0);
}

[numthreads(NumThreads_Wall, 1, 1)]
void UpdateSpatialHash_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    uint index = id.x;
    int2 cell = GetCell2D_Wall(PredictedPositions_Wall[index], smoothingRadius_Wall);
    uint hash = HashCell2D_Wall(cell);
    uint key = KeyFromHash_Wall(hash, numParticles_Wall);
    SpatialKeys_Wall[id.x] = key;
}

[numthreads(NumThreads_Wall, 1, 1)]
void Reorder_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    uint sortedIndex = SortedIndices_Wall[id.x];
    SortTarget_Positions_Wall[id.x] = Source_Positions_Wall[sortedIndex];
    SortTarget_PredictedPositions_Wall[id.x] = Source_PredictedPositions_Wall[sortedIndex];
    SortTarget_Velocities_Wall[id.x] = Source_Velocities_Wall[sortedIndex];
    SortTarget_ParticleType_Wall[id.x] = Source_ParticleType_Wall[sortedIndex];
    SortTarget_Collision_Wall[id.x] = Source_Collision_Wall[sortedIndex];
}

[numthreads(NumThreads_Wall, 1, 1)]
void ReorderCopyback_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    Positions_Wall[id.x] = CopySource_Positions_Wall[id.x];
    PredictedPositions_Wall[id.x] = CopySource_PredictedPositions_Wall[id.x];
    Velocities_Wall[id.x] = CopySource_Velocities_Wall[id.x];
    ParticleTypeBuffer_Wall[id.x] = CopySource_ParticleType_Wall[id.x];
    CollisionBuffer_Wall[id.x] = CopySource_Collision_Wall[id.x];
}

[numthreads(NumThreads_Wall, 1, 1)]
void CalculateDensities_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall || ParticleTypeBuffer_Wall[id.x][0] == -1)
        return;
    float2 pos = PredictedPositions_Wall[id.x];
    Densities_Wall[id.x] = CalculateDensity_Wall(pos);
}

[numthreads(NumThreads_Wall, 1, 1)]
void CalculatePressureForce_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall || ParticleTypeBuffer_Wall[id.x][0] == -1)
        return;
    float targetDensity = ParticleTypeBuffer_Wall[id.x][0] == 0 ? waterTargetDensity_Wall : oilTargetDensity_Wall;
    float pressureMultiplier = ParticleTypeBuffer_Wall[id.x][0] == 0 ? waterPressureMultiplier_Wall : oilPressureMultiplier_Wall;
    float nearPressureMultiplier = ParticleTypeBuffer_Wall[id.x][0] == 0 ? waterNearPressureMultiplier_Wall : oilNearPressureMultiplier_Wall;
    float density = Densities_Wall[id.x][0];
    float densityNear = Densities_Wall[id.x][1];
    float pressure = PressureFromDensity_Wall(density, targetDensity, pressureMultiplier);
    float nearPressure = NearPressureFromDensity_Wall(densityNear, nearPressureMultiplier);
    float2 pressureForce = 0;
    float2 pos = PredictedPositions_Wall[id.x];
    int2 originCell = GetCell2D_Wall(pos, smoothingRadius_Wall);
    float sqrRadius = smoothingRadius_Wall * smoothingRadius_Wall;
    for (int i = 0; i < 9; i++)
    {
        uint hash = HashCell2D_Wall(originCell + offsets2D_Wall[i]);
        uint key = KeyFromHash_Wall(hash, numParticles_Wall);
        uint currIndex = SpatialOffsets_Wall[key];
        while (currIndex < numParticles_Wall)
        {
            uint neighbourIndex = currIndex;
            currIndex++;
            if (neighbourIndex == id.x)
                continue;
            uint neighbourKey = SpatialKeys_Wall[neighbourIndex];
            if (neighbourKey != key)
                break;
            float2 neighbourPos = PredictedPositions_Wall[neighbourIndex];
            float2 offsetToNeighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);
            if (sqrDstToNeighbour > sqrRadius)
                continue;
            float neighbourDensity = Densities_Wall[neighbourIndex][0];
            if (neighbourDensity < 0.0001)
                continue;
            float neighbourNearDensity = Densities_Wall[neighbourIndex][1];
            if (neighbourNearDensity < 0.0001)
                continue;
            float dst = sqrt(sqrDstToNeighbour);
            float2 dirToNeighbour = dst > 0 ? offsetToNeighbour / dst : float2(0, 1);
            float neighbourTargetDensity = ParticleTypeBuffer_Wall[neighbourIndex][0] == 0 ? waterTargetDensity_Wall : oilTargetDensity_Wall;
            float neighbourPressureMultiplier = ParticleTypeBuffer_Wall[neighbourIndex][0] == 0 ? waterPressureMultiplier_Wall : oilPressureMultiplier_Wall;
            float neighbourNearPressureMultiplier = ParticleTypeBuffer_Wall[neighbourIndex][0] == 0 ? waterNearPressureMultiplier_Wall : oilNearPressureMultiplier_Wall;
            float neighbourPressure = PressureFromDensity_Wall(neighbourDensity, neighbourTargetDensity - density, neighbourPressureMultiplier);
            float neighbourNearPressure = NearPressureFromDensity_Wall(neighbourNearDensity, neighbourNearPressureMultiplier);
            float sharedPressure = (pressure + neighbourPressure) * 0.5;
            float sharedNearPressure = (nearPressure + neighbourNearPressure) * 0.5;
            pressureForce += dirToNeighbour * DensityDerivative_Wall(dst, smoothingRadius_Wall) * sharedPressure / neighbourDensity;
            pressureForce += dirToNeighbour * NearDensityDerivative_Wall(dst, smoothingRadius_Wall) * sharedNearPressure / neighbourNearDensity;
        }
    }
    if (density < 0.0001)
        return;
    float2 acceleration = pressureForce / density;
    Velocities_Wall[id.x] += acceleration * deltaTime_Wall;
}

[numthreads(NumThreads_Wall, 1, 1)]
void CalculateViscosity_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall || ParticleTypeBuffer_Wall[id.x][0] == -1)
        return;
    if (Densities_Wall[id.x][0] < 0.0001)
        return;
    float2 pos = PredictedPositions_Wall[id.x];
    int2 originCell = GetCell2D_Wall(pos, smoothingRadius_Wall);
    float sqrRadius = smoothingRadius_Wall * smoothingRadius_Wall;
    float2 viscosityForce = 0;
    float2 velocity = Velocities_Wall[id.x];
    for (int i = 0; i < 9; i++)
    {
        uint hash = HashCell2D_Wall(originCell + offsets2D_Wall[i]);
        uint key = KeyFromHash_Wall(hash, numParticles_Wall);
        uint currIndex = SpatialOffsets_Wall[key];
        while (currIndex < numParticles_Wall)
        {
            uint neighbourIndex = currIndex;
            currIndex++;
            if (neighbourIndex == id.x)
                continue;
            uint neighbourKey = SpatialKeys_Wall[neighbourIndex];
            if (neighbourKey != key)
                break;
            float2 neighbourPos = PredictedPositions_Wall[neighbourIndex];
            float2 offsetToNeighbour = neighbourPos - pos;
            float sqrDstToNeighbour = dot(offsetToNeighbour, offsetToNeighbour);
            if (sqrDstToNeighbour > sqrRadius)
                continue;
            float dst = sqrt(sqrDstToNeighbour);
            float2 neighbourVelocity = Velocities_Wall[neighbourIndex];
            viscosityForce += (neighbourVelocity - velocity) * ViscosityKernel_Wall(dst, smoothingRadius_Wall);
        }
    }
    float viscosityStrength = ParticleTypeBuffer_Wall[id.x][0] == 0 ? waterViscosityStrength_Wall : oilViscosityStrength_Wall;
    Velocities_Wall[id.x] += viscosityForce * viscosityStrength * deltaTime_Wall;
}

[numthreads(NumThreads_Wall, 1, 1)]
void UpdatePositions_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    ApplyCurrentForces_Wall(id.x, Positions_Wall[id.x]);
    HandleCollisions_Wall(id.x);
    Positions_Wall[id.x] += Velocities_Wall[id.x] * deltaTime_Wall;
}

[numthreads(1, 1, 1)]
void ResetCompactionCounter_Wall(uint3 id : SV_DispatchThreadID)
{
    CompactionInfoBuffer_Wall[0] = 0;
}

[numthreads(NumThreads_Wall, 1, 1)]
void CompactAndMoveParticles_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    int particleTypeIndex = Source_ParticleType_Wall[id.x].x;
    int removalFlag = Source_ParticleType_Wall[id.x].y;
    bool shouldKeep = true;

    if (removalFlag <= -2) // Ventil removal
    {
        shouldKeep = false;
        
        uint spawnIndex;
        InterlockedAdd(VentilatedParticleCount_Wall[0], 1, spawnIndex);

        if (spawnIndex < numParticles_Wall)
        {
            VentilatedParticleData data;
            data.pos = Source_Positions_Wall[id.x];
            data.vel = Source_Velocities_Wall[id.x];
            data.type = Source_ParticleType_Wall[id.x];
            data.type.y = -1;
            
            VentilatedParticlesBuffer_Wall[spawnIndex] = data;
        }
    }
    else if (removalFlag >= 0) // Player flagged this particle
    {
        if (particleTypeIndex > 0)
        {
            int particleColorIndex = particleTypeIndex - 1;
            float3 particleVisualColor = mixableColors_Wall[particleColorIndex].rgb;
            int4 nearbyObstacleIndices = Source_Collision_Wall[id.x];
            float3 uniqueColors[4];
            int uniqueColorCount = 0;
            for (int i = 0; i < 4; i++)
            {
                int obsIndex = nearbyObstacleIndices[i];
                if (obsIndex >= 0)
                {
                    float3 obsColor = ObstacleColorsBuffer_Wall[obsIndex].rgb;
                    bool alreadyFound = false;
                    for (int j = 0; j < uniqueColorCount; j++)
                    {
                        if (distance(obsColor, uniqueColors[j]) < 0.001)
                        {
                            alreadyFound = true;
                            break;
                        }
                    }
                    if (!alreadyFound && uniqueColorCount < 4)
                    {
                        uniqueColors[uniqueColorCount] = obsColor;
                        uniqueColorCount++;
                    }
                }
            }
            if (uniqueColorCount > 0)
            {
                float3 blendedColorSum = float3(0, 0, 0);
                for (int i = 0; i < uniqueColorCount; i++)
                {
                    blendedColorSum += uniqueColors[i];
                }
                float3 blendedColorAverage = blendedColorSum / uniqueColorCount;
                if (distance(blendedColorAverage, particleVisualColor) < 0.02)
                {
                    shouldKeep = false;
                }
            }
        }
    }

    if (shouldKeep)
    {
        uint newIndex;
        InterlockedAdd(CompactionInfoBuffer_Wall[0], 1, newIndex);
        SortTarget_Positions_Wall[newIndex] = Source_Positions_Wall[id.x];
        SortTarget_PredictedPositions_Wall[newIndex] = Source_PredictedPositions_Wall[id.x];
        SortTarget_Velocities_Wall[newIndex] = Source_Velocities_Wall[id.x];
        SortTarget_Collision_Wall[newIndex] = Source_Collision_Wall[id.x];
        int2 currentParticleTypeInfo = Source_ParticleType_Wall[id.x];
        currentParticleTypeInfo.y = -1;
        SortTarget_ParticleType_Wall[newIndex] = currentParticleTypeInfo;
    }
}

[numthreads(64, 1, 1)]
void CopyCollisionBuffer_Wall(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles_Wall)
        return;
    CopiedCollisionBuffer_Destination_Wall[id.x] = OriginalCollisionBuffer_Source_Wall[id.x];
}